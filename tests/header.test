#------------------------------------------------------------*- tcl -*-
# Tests for -header flag, and for proc syntax detection
#----------------------------------------------------------------------
# $Revision$
#----------------------------------------------------------------------

proc headerTestFile {body} {
    createTestFile $body
    set res [execTestFile -header _testheader_]
    set ch [open _testheader_ r]
    set data [read $ch]
    close $ch
    file delete _testheader_
    set data [split [string trim $data] \n]
    if {$res ne {%%Writing "_testheader_"}} {
        lappend data $res
    }
    return $data
}

test header-1.1 {
    Test -header flag
} -body {
    headerTestFile {
        proc x {y z} {
        }
        proc apa {nameVar x} {
            upvar $nameVar name
            set name $x
        }
    }
} -result [list "##nagelfar syntax apa n x" "##nagelfar syntax x 2"]

test header-1.2 {
    Test -header flag
} -body {
    headerTestFile {
        proc apa {nameVar x} {
            upvar $nameVar name
            return [list $name $x]
        }
        proc urga {x y {z {}}} {
            return $x
        }
    }
} -result [list "##nagelfar syntax apa v x" "##nagelfar syntax urga r 2 3"]

test header-1.3 {
    Test -header flag
} -body {
    headerTestFile {
        proc urga {x y {z {}} {a {}}} {
            return $x
        }
        proc tupp {x args} {
        }
    }
} -result [list "##nagelfar syntax tupp r 1" "##nagelfar syntax urga r 2 4"]
