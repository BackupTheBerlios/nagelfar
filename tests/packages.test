#------------------------------------------------------------*- tcl -*-
# Tests for included package definitions
#----------------------------------------------------------------------

test packages-1.1 {
    Test with included snit package definition
} -body {
    createTestFile {
        ##nagelfar syntax apa dc=_obj,apa p*
        ##nagelfar return apa _obj,apa
        ##nagelfar option apa -x
        ##nagelfar option _obj,apa\ configure -x

        ##nagelfar implicitvarns snit::type::apa self\ _obj,apa pdf

        snit::type apa {
            option -x -default 10
            variable pdf
            constructor {c d} {
                puts $c
            }
            destructor {
                list hej $pdf
            }
            method gugg {a b} {
                puts $a
            }
        }
        set x [apa %AUTO% -x 5]
        $x gugg 5 7
        $x destroy
    }
    execTestFile -flags "-s _ -s snitdb.tcl"
} -result {%%}

test packages-1.2 {
    Test with included snit package definition, auto-detect
} -body {
    createTestFile {
        package require snit
        ##nagelfar syntax apa dc=_obj,apa p*
        ##nagelfar return apa _obj,apa
        ##nagelfar option apa -x
        ##nagelfar option _obj,apa\ configure -x

        ##nagelfar implicitvarns snit::type::apa self\ _obj,apa pdf

        snit::type apa {
            option -x -default 10
            variable pdf
            constructor {c d} {
                puts $c
            }
            destructor {
                list hej $pdf
            }
            method gugg {a b} {
                puts $a
            }
        }
        set x [apa %AUTO% -x 5]
        $x gugg 5 7
        $x destroy
    }
    execTestFile -flags -pkgpicky
} -result {%%}

test packages-2.1 {
    Test with included inifile package definition
} -body {
    createTestFile {
        set f apabepa
        set fh [ini::open $f]
        ini::close $fh
        ini::close $fh miffo
    }
    execTestFile -flags "-s _ -s inifiledb.tcl"
} -result {%%Line   5: E Wrong number of arguments (2) to "ini::close"}

test packages-2.2 {
    Test with included inifile package definition
} -body {
    createTestFile {
        namespace eval ini {
            set f apabepa
            commentchar $f $f
        }
    }
    execTestFile -flags "-s _ -s inifiledb.tcl"
} -result {%%Line   4: E Wrong number of arguments (2) to "ini::commentchar"}

test packages-2.3 {
    Test with included inifile package definition, through autodetect
} -body {
    createTestFile {
        package require inifile
        set f apabepa
        set fh [ini::open $f]
        ini::close $fh
        ini::close $fh miffo
    }
    execTestFile -flags -pkgpicky
} -result {%%Line   6: E Wrong number of arguments (2) to "ini::close"}

test packages-3.1 {
    Test with included tkdnd package definition, through autodetect
} -body {
    createTestFile {
        package require tkdnd
        dnd bindtarget w x x x
        dnd cleartarget w x
    }
    execTestFile -flags -pkgpicky
} -result {%%Line   4: E Wrong number of arguments (3) to "dnd cleartarget"}

test packages-4.1 {
    Test with included vfs::mk4 package definition, through autodetect
} -body {
    createTestFile {
        package require vfs::mk4
        vfs::unmount x
        vfs::mk4::Mount x
    }
    execTestFile -flags -pkgpicky
} -result {%%Line   4: E Wrong number of arguments (1) to "vfs::mk4::Mount"}
